plugins {
  id 'java'
  id 'org.springframework.boot' version '3.4.3'
  id 'io.spring.dependency-management' version '1.1.7'
  id "org.jooq.jooq-codegen-gradle" version "3.20.1"
}

group = 'io.github.drybalka'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-jdbc'
  implementation 'org.liquibase:liquibase-core'
  implementation 'com.h2database:h2'

  implementation 'org.jooq:jooq:3.20.1'
  jooqCodegen "org.jooq:jooq-meta-extensions-liquibase:3.20.1"

  implementation 'org.mapstruct:mapstruct:1.6.3'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

  implementation 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
  useJUnitPlatform()
}

compileJava.dependsOn jooqCodegen
sourceSets.main.java.srcDirs += 'build/generated-sources/jooq'

jooq {
  configuration {
    generator {
      database {
        name = "org.jooq.meta.extensions.liquibase.LiquibaseDatabase"
        properties {
          property {
            key = "rootPath"
            value = "${project.projectDir}/src/main/resources"
          }
          property {
            key = "scripts"
            value = "liquibase.yaml"
          }
        }
      }
      generate {
        pojos = true
        pojosAsJavaRecordClasses = true
        pojosToString = false
        pojosEqualsAndHashCode = false

        daos = true
        springDao = true
        springAnnotations = true
      }
    }
  }
}

